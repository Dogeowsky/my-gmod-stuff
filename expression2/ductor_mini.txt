@name Ductor Mini v0.2.2
@inputs EGP:wirelink
@persist Plys:array

DEBUG = 1 # debug mode

function createPlyNode(I:number, Ply:entity) {
    local EIndex = I + maxPlayers()
    
    if(EGP) {
        EGP:egp3DTracker(I, vec())
        EGP:egpParent(I, Ply)
        
        EGP:egpText(EIndex, Ply:name(), vec2(0))
        EGP:egpColor(EIndex, teamColor(Ply:team()))
        EGP:egpAlign(EIndex, 1)
        EGP:egpParent(EIndex, I)
    }
}

function removePlyNode(I:number) {
    local EIndex = I + maxPlayers()
    
    if(EGP) {
        EGP:egpRemove(I)
        EGP:egpRemove(EIndex) 
    }
}

if(first()) {
    Plys = array()
    if(EGP) {
        EGP:egpClear()   
    }
    else {
        print("Link your EGP and reset the chip!")   
    }
    
    foreach(Key:number, Ply:entity = players()) {
        if(Ply != owner()) {
            Plys:pushEntity(Ply)
            
            createPlyNode(Key, Ply)
            
            if(DEBUG) {
                print(Ply)
            }            
        }
    }
}

event playerConnected(ConnectedPly:entity) {
    timer("awaitPlayerInit", 0.2, function() {
        Plys:pushEntity(ConnectedPly)
        
        createPlyNode(Plys:count(), ConnectedPly)
    })  
}

event playerDisconnected(DisconnectedPly:entity) {
    foreach(Key:number, Ply:entity = Plys) {
        if(DisconnectedPly == Ply) {
            Plys:removeEntity(Key)
            
            removePlyNode(Key)
            
            break
        }   
    }
}

event keyPressed(Player:entity, Key:string, Down:number, _:string) {
    if(Player == owner()) {
        if(Key == "g" && Down) {
            printTable(Plys)
        }
    }   
}

# by Pompin84 (Dogeowsky)
